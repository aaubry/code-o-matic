COMPILER Selectors

IGNORECASE

CHARACTERS
	symbolchar = "0123456789ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz-_".
	iterationchar = '*'.
	memberseparatorchar = '.'.
	selectorseparatorchar = ','.
	whitespace = " \t\r\n".

TOKENS
	symbol = symbolchar { symbolchar }.
	iteration = iterationchar.
	memberseparator = memberseparatorchar.
	selectorseparator = selectorseparatorchar.

IGNORE whitespace

PRODUCTIONS

Selectors =															(. StartParsingSelectors(); .)
	(
		Selector
	|
																	(. AddSelector(new SelectorPartList()); .)
	)
	{ selectorseparator Selector }.

Selector =
																	(. var parts = new SelectorPartList(); .)
	SelectorSegment<parts>
	{
		memberseparator
		SelectorSegment<parts>
	}																(. AddSelector(parts); .)
	.
	
SelectorSegment<SelectorPartList parts> =							(. SelectorPart part; .)
	(
		Member<out part>											(. parts.Add(part); .)
		[
			Iteration<out part>										(. parts.Add(part); .)
		]
	|
		Iteration<out part>											(. parts.Add(part); .)
	)
	.

Iteration<out SelectorPart part> =
	iteration														(. part = IterationSelectorPart.Instance; .)
	.

Member<out SelectorPart part> =
	symbol															(. part = new MemberSelectorPart(t.val); .)
	.

END Selectors.
